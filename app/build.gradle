apply plugin: 'com.android.application'
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'
apply plugin: 'project-report'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:1.0.2'
    }
}

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "tier"
    productFlavors {
        free {
            dimension "tier"
            applicationIdSuffix ".free"
        }
        paid {
            dimension "tier"
            applicationIdSuffix ".paid"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    endpointsServer project(path: ':backend', configuration: 'endpoints')
    implementation project(':jokerlib')
    implementation project(':androidjoker')
    implementation project(':backend')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    freeImplementation 'com.google.android.gms:play-services-ads:15.0.1'
    implementation('com.google.api-client:google-api-client-android:1.23.0') {
        exclude module: 'guava-jdk5'
    }
    implementation 'com.google.http-client:google-http-client-android:1.23.0'
    implementation 'com.google.code.findbugs:jsr305:3.0.1'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation ('com.android.support.test.espresso:espresso-intents:3.0.2'){
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    implementation 'com.android.support.test.espresso:espresso-idling-resource:3.0.2'
    testImplementation 'junit:junit:4.12'
}

task startAppEngine{
    dependsOn ':backend:appengineStart'
    doLast {
        println 'Server was started'
    }
}

task stopAppEngine{
    dependsOn ':backend:appengineStop'
    doLast{
        println 'Server was stopped'
    }
}

task runTests{
    dependsOn 'test', 'connectedAndroidTest'
}

task executeTests{
    dependsOn startAppEngine
    finalizedBy runTests, stopAppEngine
}


